#ifndef LIGHTING_H
#define LIGHTING_H

#include "../rendersettings.glh"

uniform sampler2D normalsAndSpecSampler;
uniform sampler2D shadowSampler;

layout(std140) uniform LightUB
{
	vec3 color;
	float extra;
	float attenLin;
	float attenExp;
	float flickerIntensity;
	float flickerOffset;
} light;

layout(std140) uniform ShadowRenderSettingsUB
{
	mat3 shadowViewMatrix;
};

struct GBufferData
{
	vec3 normal;
	float specIntensity;
	float specExponent;
};

float getShadowFactor(vec2 worldPos)
{
	vec2 samplePos = (shadowViewMatrix * vec3(worldPos, 1.0)).xy / 2.0 + vec2(0.5);
	if (samplePos.x < 0 || samplePos.y < 0 || samplePos.x > 1 || samplePos.y > 1)
		return 0.0;
	return 1.0 - texture(shadowSampler, samplePos).r;
}

float getFlickerFactor(float offset, float intensity)
{
	return 1.0 - (sin(time * 40 + offset) + 1) * 0.5 * intensity;
}

vec3 smDecode(vec2 enc)
{
	vec2 fenc = enc * 4.0 - 2.0;
	float f = dot(fenc, fenc);
	float g = sqrt(1.0 - f / 4.0);
	vec3 n;
	n.xy = fenc * g;
	n.z = 1.0 - f / 2.0;
	return n;
}

GBufferData getGBufferData(vec2 texCoord)
{
	GBufferData data;
	
	vec4 normalAndSpec = texture(normalsAndSpecSampler, texCoord);
	
	data.normal = normalize(smDecode(normalAndSpec.xy));
	
	data.specIntensity = normalAndSpec.z * 100.0;
	data.specExponent = normalAndSpec.w * 100.0;
	
	return data;
}

float phong(vec3 lightIn, vec3 cameraPos, vec2 worldPos, GBufferData data)
{
	float diffuseFactor = max(dot(data.normal, -lightIn), 0.0);
	
	vec3 dirToEye = normalize(cameraPos - vec3(worldPos.x, 0.0, worldPos.y));
	vec3 reflectDir = reflect(lightIn, data.normal);
	
	float specFactor = max(pow(dot(dirToEye, reflectDir), data.specExponent), 0.0);
	
	return diffuseFactor * (data.specIntensity * specFactor + 1.0);
}

#endif
